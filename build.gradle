buildscript {
    ext {
        springBootVersion = '2.1.6.RELEASE'
        spotBugsVersion = '2.0.0'
    }

    repositories {
        mavenCentral()

        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:${spotBugsVersion}")
    }
}

plugins {
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'com.palantir.git-version' version '0.12.0-rc2'
}

allprojects {

    apply plugin: 'java'
    apply plugin: "com.github.spotbugs"
    apply plugin: 'jacoco'

    group = 'xyz.aguang'
    version = '1.0.0.SNAPSHOT'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    spotbugs {

        reportLevel = 'high'
    }

    spotbugsMain {

        reports {

            xml {

                enabled false
            }

            html {

                enabled true

                destination file("build/reports/spotbugs/${project.name}.html")
            }
        }
    }
}

project(':rest-rpc-contract') {
}

project(':rest-rpc-registry') {

    apply plugin: 'org.springframework.boot'

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

        compile(project(':rest-rpc-contract'))
    }

    javadoc.dependsOn check

    bootJar.dependsOn check
    bootRun.dependsOn check

    task dockerBuild {
        dependsOn bootJar

        doLast {
            def dockerfile = file("${projectDir}/build/tmp/Dockerfile")

            dockerfile.text = 'FROM openjdk:11-slim\n'
            dockerfile.text += 'WORKDIR /workspace\n'
            dockerfile.text += "COPY build/libs/${project.name}-${version}.jar ./\n"
            dockerfile.text += "CMD [\"java\", \"-jar\", \"${project.name}-${version}.jar\"]"

            exec {
                commandLine 'docker', 'build', '-t', "${project.name}:${version}", '-f', "${projectDir}/build/tmp/Dockerfile", "${projectDir}"
            }
        }
    }

    task dockerRun {
        dependsOn dockerBuild

        doLast {
            exec {
                commandLine 'docker', 'run', "${project.name}:${version}"
            }
        }
    }
}

project(':rest-rpc-client-java') {

    dependencies {
        compile('com.alibaba:fastjson:1.2.58')
        compile('org.slf4j:slf4j-api:1.7.26')

        compile(project(':rest-rpc-contract'))
    }
}

project(':rest-rpc-client-spring') {

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

        compile(project(':rest-rpc-client-java'))
    }
}

task doc(type: Javadoc) {

    dependsOn build

    source getSubprojects().collect { it.sourceSets.main.allJava }

    classpath = files(getSubprojects().collect { it.sourceSets.main.compileClasspath })

    destinationDir = file("${projectDir}/docs/javadocs")

    options {

        noTimestamp = true
    }
}
